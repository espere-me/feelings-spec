plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.0'
}

repositories {
    jcenter()
}

dependencies {
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.16.20'

    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.16.20'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.9.1'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.16.0'
}

publishing {
    publications {
        feelingsSpec(MavenPublication) {
            from components.java
            groupId 'me.espere.feelings'
            artifactId 'feelings-spec'
        }
    }
}

bintray {
    dryRun = !project.hasProperty('deploy')

    user System.getenv('BINTRAY_USER')
    key System.getenv('BINTRAY_KEY')

    pkg {
        repo = 'production'
        name = 'feelings-spec'
        userOrg = 'espere-me'

        version {
            name = project.property('version')
        }
    }

    publications = ['feelingsSpec']

    publish = true
}

task checkVersion {
    description 'This task fails the build if the version property is not set'

    doLast {
        if (project.property('version') == 'unspecified') {
            throw new GradleException('You must specify a version property')
        }
    }
}

tasks.bintrayUpload.dependsOn checkVersion
tasks.bintrayUpload.dependsOn clean
tasks.bintrayUpload.dependsOn build

clean.mustRunAfter checkVersion
build.mustRunAfter clean
